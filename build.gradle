plugins {
	id 'org.springframework.boot' version '2.6.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'org.hidetake.swagger.generator' version '2.19.2'
}

group = 'com.learn.packt.modernapi'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	compileOnly 'org.springframework.boot:spring-boot-starter-validation'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	compileOnly 'org.projectlombok:lombok:1.18.22'
	annotationProcessor 'org.projectlombok:lombok:1.18.22'
	testCompileOnly 'org.projectlombok:lombok:1.18.22'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

	swaggerCodegen 'org.openapitools:openapi-generator-cli:4.3.1'
	swaggerUI 'org.webjars:swagger-ui:3.52.5'
	compileOnly 'io.swagger:swagger-annotations:1.6.2'
	compileOnly 'org.openapitools:jackson-databind-nullable:0.2.1'

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.flywaydb:flyway-core'
	runtimeOnly 'com.h2database:h2'
}

swaggerSources {
	def typeMappings = 'URI=URI'
	def importMappings = 'URI=java.net.URI'
	modernapi {
		def apiYaml = "${rootDir}/src/main/resources/api/apispec.yaml"
		def configJson = "${rootDir}/src/main/resources/api/config.json"
		inputFile = file(apiYaml)
		def ignoreFile = file("${rootDir}/src/main/resources/api/.openapi-generator-ignore")
		code {
			language = 'spring'
			configFile = file(configJson)
			rawOptions = ['--ignore-file-override', ignoreFile, '--type-mappings',
						  typeMappings, '--import-mappings', importMappings] as List<String>
			components = [models: true, apis: true, supportingFiles: 'ApiUtil.java']
			dependsOn validation
		}
	}
}

compileJava.dependsOn swaggerSources.modernapi.code
sourceSets.main.java.srcDir "${swaggerSources.modernapi.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.modernapi.code.outputDir}/src/main/resources"

tasks.named('test') {
	useJUnitPlatform()
}
